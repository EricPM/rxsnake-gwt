apply plugin: 'java'
apply plugin: 'maven'

group = 'com.intendia'
version = '1.0-SNAPSHOT'

description = """Snake game in GWT using reactive programming style powered by RxJava GWT."""

sourceCompatibility = JavaVersion.VERSION_1_10
targetCompatibility = JavaVersion.VERSION_1_10
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/google-snapshots/' }
    maven { url = 'https://raw.githubusercontent.com/intendia-oss/rxjava-gwt/mvn-repo/' }
}

dependencies {
    compile 'com.google.gwt:gwt-user'
    compile 'com.google.gwt:gwt-dev'
    compile 'com.intendia.gwt:rxjava2-gwt:2.1.2-beta1'
    compile 'com.intendia.gwt.rxgwt2:rxgwt:2.1'
    compile 'org.jboss.gwt.elemento:elemento-core:0.7.0'
    compile 'com.google.code.findbugs:jsr305:3.0.2'
}

configurations.all {
    resolutionStrategy {
        force 'com.google.gwt:gwt-user:HEAD-SNAPSHOT',
                'com.google.gwt:gwt-dev:HEAD-SNAPSHOT',
                'com.google.jsinterop:jsinterop-annotations:HEAD-SNAPSHOT',
                'com.google.jsinterop:base:1.0.0-RC1',
                'org.ow2.asm:asm:6.1.1',
                'org.ow2.asm:asm-util:6.1.1',
                'org.ow2.asm:asm-commons:6.1.1'
    }
}

task gwtCompile(type: JavaExec) {
    dependsOn classes
    description = 'GWT compiler'
    def sources = files(sourceSets.main.java.srcDirs, sourceSets.main.resources.srcDirs, sourceSets.main.compileClasspath)
    inputs.files(sources)
    outputs.dir("$project.buildDir/gwtCompile/war")
    workingDir "$project.buildDir/gwtCompile"
    doFirst { workingDir.mkdirs() }
    main = 'com.google.gwt.dev.Compiler'
    classpath sources
    args = ['-sourceLevel', '10', '-draftCompile', '-failOnError', 'rxsnake.RxSnake']
}

task gwtServe(type: JavaExec) {
    dependsOn classes
    description = 'GWT serve'
    workingDir "$project.buildDir/gwtServe"
    doFirst { workingDir.mkdirs() }
    main = 'com.google.gwt.dev.DevMode'
    classpath files(sourceSets.main.java.srcDirs, sourceSets.main.resources.srcDirs, sourceSets.main.compileClasspath)
    args = ['-sourceLevel', '10', '-failOnError', 'rxsnake.RxSnake']
}
