apply plugin: 'java'
apply plugin: 'maven'

group = 'com.intendia'
version = '1.0-SNAPSHOT'

description = """Snake game in GWT using reactive programming style powered by RxJava GWT."""

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/google-snapshots' }
    maven { url = 'https://raw.githubusercontent.com/intendia-oss/rxjava-gwt/mvn-repo/' }
    mavenCentral()
}

dependencies {
    compile group: 'com.google.gwt', name: 'gwt-user', version: '2.8.2-rx1'
    compile group: 'com.google.gwt', name: 'gwt-dev', version: '2.8.2-rx1'
    compile group: 'com.intendia.gwt', name: 'rxjava2-gwt', version: '2.1.2-beta1'
    compile group: 'com.intendia.gwt.rxgwt2', name: 'rxgwt', version: '2.1'
    compile group: 'org.jboss.gwt.elemento', name: 'elemento-core', version: '0.7.0'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
}

task gwtCompile(type: JavaExec) {
    dependsOn classes
    description = 'GWT compiler'
    def sources = files(sourceSets.main.java.srcDirs, sourceSets.main.resources.srcDirs, sourceSets.main.compileClasspath)
    inputs.files(sources)
    outputs.dir("$project.buildDir/gwtCompile/war")
    workingDir "$project.buildDir/gwtCompile"
    doFirst { workingDir.mkdirs() }
    main = 'com.google.gwt.dev.Compiler'
    classpath sources
    args = ['-draftCompile', '-failOnError', 'rxsnake.RxSnake']
}

task gwtServe(type: JavaExec) {
    dependsOn classes
    description = 'GWT serve'
    workingDir "$project.buildDir/gwtServe"
    doFirst { workingDir.mkdirs() }
    main = 'com.google.gwt.dev.DevMode'
    classpath files(sourceSets.main.java.srcDirs, sourceSets.main.resources.srcDirs, sourceSets.main.compileClasspath)
    args = ['-failOnError', 'rxsnake.RxSnake']
}
