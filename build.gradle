plugins {
    id 'java'
    id 'maven'
    id 'org.ajoberstar.git-publish' version '1.0.1'
}

group = 'com.intendia'
version = '1.0-SNAPSHOT'

description = """Snake game in GWT using reactive programming style powered by RxJava GWT."""

sourceCompatibility = JavaVersion.VERSION_1_10
targetCompatibility = JavaVersion.VERSION_1_10
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/google-snapshots/' }
    maven { url = 'https://raw.githubusercontent.com/intendia-oss/rxjava-gwt/mvn-repo/' }
    mavenLocal()
}

dependencies {
    implementation 'com.google.gwt:gwt-user:HEAD-SNAPSHOT'
    implementation 'com.google.gwt:gwt-dev:HEAD-SNAPSHOT'
    implementation 'com.intendia.gwt.rxgwt2:rxgwt:HEAD-SNAPSHOT'
    implementation 'org.jboss.gwt.elemento:elemento-core:0.8.2-gwt2'
}

private gwtClasspath() {
    files(sourceSets.main.java.srcDirs, sourceSets.main.resources.srcDirs, sourceSets.main.compileClasspath)
}

task gwtCompile(type: JavaExec) {
    dependsOn classes
    description = 'GWT compiler'
    inputs.files(gwtClasspath())
    outputs.dir("$project.buildDir/gwtCompile/war/rxsnake")
    workingDir "$project.buildDir/gwtCompile"
    doFirst { workingDir.mkdirs() }
    main = 'com.google.gwt.dev.Compiler'
    classpath gwtClasspath()
    args = ['-sourceLevel', '10', '-draftCompile', '-failOnError', 'rxsnake.RxSnake']
}

task gwtServe(type: JavaExec) {
    dependsOn classes
    description = 'GWT serve'
    workingDir "$project.buildDir/gwtServe"
    doFirst { workingDir.mkdirs() }
    main = 'com.google.gwt.dev.DevMode'
    classpath gwtClasspath()
    args = ['-sourceLevel', '10', '-failOnError', 'rxsnake.RxSnake']
}

gitPublish {
    repoUri = 'git@github.com:ibaca/rxsnake-gwt.git'
    branch = 'gh-pages'
    contents {
        from(gwtCompile)
    }
}
